# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=with-includes
pkgver=0.0.1
pkgrel=1
pkgdesc="Sample transactions with include directives"
arch=('any')
makedepends=('mktxn')
optdepends=('nightscape: double-entry bookkeeping TXN reporting and analysis')
groups=('nightscape' 'txn')
source=("with-includes.tar.gz"{,.asc})
sha256sums=('048ab635afc6700af84e3f3ea5d89262ab51ea1c7bac2a230210e44ac0141deb'
            'SKIP')
validpgpkeys=('DA996E276B1F51FBC492E9873DBB92CDB1B08DAB')

_dir='/var/cache/txn/pkg'

prepare() {
  cd "$srcdir/$pkgname"

  msg2 'Preparing TXNBUILD...'
  rm -f TXNBUILD
  cat >> TXNBUILD <<EOF
pkgname = '$pkgname'
pkgver = '$pkgver'
pkgrel = $pkgrel
pkgdesc = '$pkgdesc'
source = '$pkgname.txn'
EOF
}

build() {
  cd "$srcdir/$pkgname"

  msg2 'Building...'
  mktxn
}

package() {
  cd "$srcdir/$pkgname"

  msg2 'Installing...'
  install -Dm 644 "$pkgname.txn" -t "$pkgdir/usr/include/txn"
  cp -dpr --no-preserve=ownership includes "$pkgdir/usr/include/txn"
  install -Dm 644 "$pkgname-$pkgver-$pkgrel.txn.pkg.tar.xz" -t "$pkgdir/$_dir"
}
