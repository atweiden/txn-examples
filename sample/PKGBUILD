# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=sample
pkgver=0.1.9
pkgrel=1
pkgdesc="Sample transactions"
arch=('any')
makedepends=('mktxn')
optdepends=('nightscape: double-entry bookkeeping TXN reporting and analysis')
groups=('nightscape' 'txn')
source=("sample.tar.gz")
sha256sums=('50959d019ef883fec3400df213204228352eda2e9fb7675a66efa8caaee3089b')

_user="$(id -un)"
_group="$(id -gn)"
_txn_dir="$HOME/.txn"
_include_dir="$_txn_dir/include"
_package_dir="$_txn_dir/package"

prepare() {
  cd "$srcdir/$pkgname"

  msg2 'Preparing TXNBUILD...'
  rm -f TXNBUILD
  cat >> TXNBUILD <<EOF
pkgname = '$pkgname'
pkgver = '$pkgver'
pkgrel = $pkgrel
pkgdesc = '$pkgdesc'
source = '$pkgname.txn'
EOF

  msg2 'Preparing pacman hooks...'
  cat >> "$pkgname-01-chown.hook" <<EOF
[Trigger]
Operation = Install
Operation = Upgrade
Type = Package
Target = $pkgname

[Action]
Description = Setting txn directory permissions...
Depends = coreutils
When = PostTransaction
Exec = /usr/bin/chown -R $_user:$_group $_txn_dir
EOF
}

build() {
  cd "$srcdir/$pkgname"

  msg2 'Building...'
  mktxn
}

package() {
  cd "$srcdir/$pkgname"

  msg2 'Installing...'
  install -dm 700 "$pkgdir/$_include_dir"
  install -dm 700 "$pkgdir/$_package_dir"
  install -Dm 644 "$pkgname.txn" -t "$pkgdir/$_include_dir"
  install -Dm 644 "$pkgname-$pkgver-$pkgrel.txn.pkg.tar.xz" \
          -t "$pkgdir/$_package_dir"

  msg2 'Installing pacman hooks...'
  install -Dm 644 *.hook -t "$pkgdir/usr/share/libalpm/hooks"
}
