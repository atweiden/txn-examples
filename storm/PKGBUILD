# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=storm
pkgver=0.0.1
pkgrel=1
pkgdesc="Storm transactions"
arch=('any')
makedepends=('mktxn')
optdepends=('nightscape: double-entry bookkeeping TXN reporting and analysis')
groups=('nightscape' 'txn')
source=("storm.tar.gz"{,.asc})
sha256sums=('048ab635afc6700af84e3f3ea5d89262ab51ea1c7bac2a230210e44ac0141deb'
            'SKIP')
validpgpkeys=('DA996E276B1F51FBC492E9873DBB92CDB1B08DAB')

_user="$(id -un)"
_group="$(id -gn)"
_include_dir="$HOME/.txn/include"
_package_dir="$HOME/.txn/package"

prepare() {
  cd "$srcdir/$pkgname"

  msg2 'Preparing TXNBUILD...'
  rm -f TXNBUILD
  cat >> TXNBUILD <<EOF
pkgname = '$pkgname'
pkgver = '$pkgver'
pkgrel = $pkgrel
pkgdesc = '$pkgdesc'
source = '$pkgname.txn'
EOF
}

build() {
  cd "$srcdir/$pkgname"

  msg2 'Building...'
  mktxn
}

package() {
  cd "$srcdir/$pkgname"

  msg2 'Installing...'
  install -dm 700 "$pkgdir/$_include_dir"
  install -dm 700 "$pkgdir/$_package_dir"
  install -Dm 644 "$pkgname.txn" -t "$pkgdir/$_include_dir"
  install -Dm 644 "$pkgname-$pkgver-$pkgrel.txn.pkg.tar.xz" \
          -t "$pkgdir/$_package_dir"
  find "$pkgdir" -mindepth 1 -maxdepth 1 -exec chown -R "$_user":"$_group" '{}' +
}
