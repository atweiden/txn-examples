# Maintainer: Andy Weidenbaum <atweiden@tutanota.de>

pkgname=lot-sales
pkgver=0.0.1
pkgrel=1
pkgdesc="Lot sales example"
arch=('any')
makedepends=('mktxn')
optdepends=('tantum: double-entry accounting system')
groups=('tantum' 'txn')
source=("lot-sales.tar.gz")
sha256sums=('4ce06d62ea50e377fd30f777a2b5b8fc068395fc61567733a6e6d8a0351e1cf5')

_user="$(id -un)"
_group="$(id -gn)"
_txn_dir="$HOME/.txn"
_include_dir="$_txn_dir/include"
_package_dir="$_txn_dir/package"

prepare() {
  cd "$srcdir/$pkgname"

  msg2 'Preparing TXNBUILD...'
  rm -f TXNBUILD
  cat >> TXNBUILD <<EOF
pkgname = '$pkgname'
pkgver = '$pkgver'
pkgrel = $pkgrel
pkgdesc = '$pkgdesc'
source = '$pkgname.txn'
EOF

  msg2 'Preparing pacman hooks...'
  cat >> "$pkgname-01-chown.hook" <<EOF
[Trigger]
Operation = Install
Operation = Upgrade
Type = Package
Target = $pkgname

[Action]
Description = Setting txn directory permissions...
Depends = coreutils
When = PostTransaction
Exec = /usr/bin/chown -R $_user:$_group $_txn_dir
EOF
}

build() {
  cd "$srcdir/$pkgname"

  msg2 'Building...'
  mktxn
}

package() {
  cd "$srcdir/$pkgname"

  msg2 'Installing...'
  install -dm 700 "$pkgdir/$_include_dir"
  install -dm 700 "$pkgdir/$_package_dir"
  install -Dm 644 "$pkgname.txn" -t "$pkgdir/$_include_dir"
  install -Dm 644 "$pkgname-$pkgver-$pkgrel.txn.pkg.tar.xz" \
          -t "$pkgdir/$_package_dir"

  msg2 'Installing pacman hooks...'
  install -Dm 644 *.hook -t "$pkgdir/usr/share/libalpm/hooks"
}
