# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=fy2014
pkgver=0.8.8
pkgrel=1
pkgdesc="FY2014 transaction journal"
arch=('any')
depends=('fy2013')
makedepends=('mktxn')
optdepends=('nightscape: double-entry bookkeeping TXN reporting and analysis')
groups=('nightscape' 'txn')
source=("fy2014.tar.gz")
sha256sums=('fe3f03964452b756f415d398e311c5f0b00761cf8644f0f655c44a9767bff468')

_user="$(id -un)"
_group="$(id -gn)"
_txn_dir="$HOME/.txn"
_include_dir="$_txn_dir/include"
_package_dir="$_txn_dir/package"

prepare() {
  cd "$srcdir/$pkgname"

  msg2 'Preparing TXNBUILD...'
  rm -f TXNBUILD
  cat >> TXNBUILD <<EOF
pkgname = '$pkgname'
pkgver = '$pkgver'
pkgrel = $pkgrel
pkgdesc = '$pkgdesc'
source = '$pkgname.txn'
EOF

  msg2 'Preparing pacman hooks...'
  cat >> "$pkgname-01-chown.hook" <<EOF
[Trigger]
Operation = Install
Operation = Upgrade
Type = Package
Target = $pkgname

[Action]
Description = Setting txn directory permissions...
Depends = coreutils
When = PostTransaction
Exec = /usr/bin/chown -R $_user:$_group $_txn_dir
EOF
}

build() {
  cd "$srcdir/$pkgname"

  msg2 'Building...'
  mktxn
}

package() {
  cd "$srcdir/$pkgname"

  msg2 'Installing...'
  install -dm 700 "$pkgdir/$_include_dir"
  install -dm 700 "$pkgdir/$_package_dir"
  install -Dm 644 "$pkgname.txn" -t "$pkgdir/$_include_dir"
  install -Dm 644 "$pkgname-$pkgver-$pkgrel.txn.pkg.tar.xz" \
          -t "$pkgdir/$_package_dir"

  msg2 'Installing pacman hooks...'
  install -Dm 644 *.hook -t "$pkgdir/usr/share/libalpm/hooks"
}
