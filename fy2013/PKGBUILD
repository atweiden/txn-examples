# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=fy2013
pkgver=1.0.0
pkgrel=1
pkgdesc="FY2013 transaction journal"
arch=('any')
makedepends=('mktxn')
optdepends=('nightscape: double-entry bookkeeping TXN reporting and analysis')
groups=('nightscape' 'txn')
source=("fy2013.tar.gz")
sha256sums=('029a9eb6af4bfcaa4c54b8a73415c261738c96b5681b15d478b6c824598421b7')

_user="$(id -un)"
_group="$(id -gn)"
_include_dir="$HOME/.txn/include"
_package_dir="$HOME/.txn/package"

prepare() {
  cd "$srcdir/$pkgname"

  msg2 'Preparing TXNBUILD...'
  rm -f TXNBUILD
  cat >> TXNBUILD <<EOF
pkgname = '$pkgname'
pkgver = '$pkgver'
pkgrel = $pkgrel
pkgdesc = '$pkgdesc'
source = '$pkgname.txn'
EOF

  msg2 'Preparing pacman hooks...'
  cat >> "$pkgname-01-chown.hook" <<EOF
[Trigger]
Operation = Install
Operation = Upgrade
Type = Package
Target = $pkgname

[Action]
Description = Setting txn directory permissions...
Depends = coreutils
When = PostTransaction
Exec = /usr/bin/chown -R "$_user":"$_group" "$_include_dir" "$_package_dir"
EOF
}

build() {
  cd "$srcdir/$pkgname"

  msg2 'Building...'
  mktxn
}

package() {
  cd "$srcdir/$pkgname"

  msg2 'Installing...'
  install -dm 700 "$pkgdir/$_include_dir"
  install -dm 700 "$pkgdir/$_package_dir"
  install -Dm 644 "$pkgname.txn" -t "$pkgdir/$_include_dir"
  install -Dm 644 "$pkgname-$pkgver-$pkgrel.txn.pkg.tar.xz" \
          -t "$pkgdir/$_package_dir"

  msg2 'Installing pacman hooks...'
  install -Dm 644 *.hook -t "$pkgdir/usr/share/libalpm/hooks"
}
