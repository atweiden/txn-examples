# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=fy2013
pkgver=1.0.0
pkgrel=1
pkgdesc="FY2013 transaction journal"
arch=('any')
makedepends=('mktxn')
optdepends=('nightscape: double-entry bookkeeping TXN reporting and analysis')
groups=('nightscape' 'txn')
source=("fy2013.tar.gz"{,.asc})
sha256sums=('029a9eb6af4bfcaa4c54b8a73415c261738c96b5681b15d478b6c824598421b7'
            'SKIP')
validpgpkeys=('DA996E276B1F51FBC492E9873DBB92CDB1B08DAB')

_dir='/var/cache/txn/pkg'

prepare() {
  cd "$srcdir/$pkgname"

  msg2 'Preparing TXNBUILD...'
  rm -f TXNBUILD
  cat >> TXNBUILD <<EOF
pkgname = '$pkgname'
pkgver = '$pkgver'
pkgrel = $pkgrel
pkgdesc = '$pkgdesc'
source = '$pkgname.txn'
EOF
}

build() {
  cd "$srcdir/$pkgname"

  msg2 'Building...'
  mktxn
}

package() {
  cd "$srcdir/$pkgname"

  msg2 'Installing...'
  install -Dm 644 "$pkgname.txn" -t "$pkgdir/usr/include/txn"
  install -Dm 644 "$pkgname-$pkgver-$pkgrel.txn.pkg.tar.xz" -t "$pkgdir/$_dir"
}
